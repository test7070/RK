ALTER  function [dbo].[z_cub_rkp04](@t_bdate nvarchar(max), @t_edate nvarchar(max), @t_makeno nvarchar(max))
returns @tmp table(
	sel int identity(1,1)
	,gno nvarchar(20)
	,pno int
	,recno int
	,makeno nvarchar(30)
	,tmakeno nvarchar(30)
	,uno nvarchar(30)
	,dime float
	,width float
	,lengthb float
	,radius float
	,pvcno nvarchar(20)
	,pvc nvarchar(30)
	,custno nvarchar(20)
	,cust nvarchar(50)
	,size nvarchar(20)
	,ordeno nvarchar(20)
	,ordeno2 nvarchar(10)
	,mins float
	
	,date_cub nvarchar(20) -- cub date
	,[weight] float
	,price float
	,total float
	
	,date_cut nvarchar(20) -- cut date
	--期初存貨
	,weight01 float
	,price01 float
	,total01 float			
	--盤點調整	
	,weight02 float
	,price02 float
	,total02 float			
	--完工投入	
	,weight03 float
	,price03 float
	,total03 float			
	--耗料	
	,weight04 float
	,price04 float
	,total04 float			
	--樣品	
	,weight05 float
	,price05 float
	,total05 float			
	--報廢	
	,weight06 float
	,price06 float
	,total06 float		
	--領用
	,weight07 float		
	,price07 float
	,total07 float
	--期末存貨	
	,weight10 float
	,price10 float
	,total10 float	
	
	,accy nvarchar(10)
	,noa nvarchar(20)
	,noq nvarchar(10)
	,spec nvarchar(max)
	,cspec nvarchar(max)
	,x01 float
	,y01 float
) as
begin
	insert into @tmp(gno,pno,makeno,uno,date_cub,[weight],total
		,dime,width,lengthb,radius,ordeno,ordeno2,custno,cust,size
		,mins,accy,noa,noq
		,x01,y01)
	select '1',1,a.makeno,isnull(a.uno,''),b.datea,a.hweight
		--原物料成本+費用
		,isnull(a.total,0) 
		,a.dime,a.width,a.lengthb,a.radius,a.ordeno,a.no2,a.custno,a.comp,a.size
		,case when b.datea>=@t_bdate then a.mins else 0 end
		,a.accy,a.noa,a.noq
		,a.x01,a.y01
	from view_cubs a
	left join view_cub b on a.accy=b.accy and a.noa=b.noa
	where b.datea<=@t_edate
	and (len(@t_makeno)=0 or a.makeno=@t_makeno or LEFT(a.makeno,1)='R') --因為要算再投入的,所以R的也都要算,之後再刪

	--規格
	update @tmp set spec = b.spec,cspec=c.product
	from @tmp a
	left join view_ordes b on a.ordeno=b.noa and a.ordeno2=b.no2
	left join spec c on b.spec=c.noa
	where b.noa is not null	

	--皮膜、保護膜
	update @tmp set pvcno=ISNULL(b.productno,''),pvc=ISNULL(b.product,'')
	from @tmp a
	outer apply (select top 1 productno,product from view_cubt where noa=a.noa and nor=a.noq and kind='1' order by noq) b
	
	--沒有皮膜就改顯示面漆
	update @tmp set pvcno=d.noa,pvc=d.product
	from @tmp a
	left join view_cubs b on a.makeno=b.makeno
	left join view_cubu	c on b.accy=c.accy and b.noa=c.noa
	left join ucc d on c.productno=d.noa
	where d.groupano = 'A05'
	
	--皮膜(再投入)	
	update @tmp set pvcno=ISNULL(b.spec,a.pvcno),pvc=ISNULL(b.class,a.pvc)
	from @tmp a
	left join view_cuts b on a.uno=b.bno
	where len(a.uno)>0 
	and b.noa is not null

	update @tmp set price = case when [weight]!=0 then round(total/[weight],3) else 0 end
	update @tmp set date_cut=ISNULL(b.datea,'')
	from @tmp a
	outer apply(select top 1 * from view_cuts where a.makeno=cname order by datea) b
	--前期庫存
	declare @bbdate nvarchar(20) = dbo.AD2ChineseEraName( dateadd(DD,-1,dbo.ChineseEraName2AD(@t_bdate)))
	declare @t_ecustno nvarchar(max) = char(255)
 
	update @tmp set weight01=isnull(b.w23,0),total01=isnull(b.emoney,0)
	from @tmp a
	left join dbo.z_cub_rk02('',@bbdate,'','','',@t_ecustno) b on a.makeno=b.makeno

	
	-- 期末存貨
	update @tmp set weight10=ISNULL(b.w23,0),total10=isnull(b.emoney,0)
	from @tmp a
	left join dbo.z_cub_rk02(@t_bdate,@t_edate,'','','',@t_ecustno) b on a.makeno=b.makeno


	--本期盤點調整
	update @tmp set weight02=ISNULL(b.weight11,0)
		,total02=ISNULL(b.m04,0)
	from @tmp a
	outer apply(select SUM(isnull(x.weight11,0)) weight11 
		,SUM(isnull(x.m04,0)) m04
		from view_cucs x 
		left join view_cuc y on x.accy=y.accy and x.noa=y.noa 
		where y.noa is not null and x.cubno=a.makeno and y.datea between @t_bdate and @t_edate) b
	
	--本期完工投入
	update @tmp set weight03=[weight],total03=total
		,price03=case when ISNULL([weight],0)=0 then price else round(total/[weight],4) end 
		where date_cub>=@t_bdate
		
	--本期樣品、本期報廢
	update @tmp set weight05=ISNULL(b.weight6,0)
		,weight06=ISNULL(b.weight7,0)
		,total05=ISNULL(b.m02,0)
		,total06=ISNULL(b.m03,0)
	from @tmp a
	outer apply(select SUM(isnull(x.weight6,0)) weight6 
		,SUM(isnull(x.weight7,0)) weight7 
		,SUM(isnull(x.m02,0)) m02 
		,SUM(isnull(x.m03,0)) m03 
		from view_cucs x 
		left join view_cuc y on x.accy=y.accy and x.noa=y.noa 
		where y.noa is not null and x.cubno=a.makeno and y.datea between @t_bdate and @t_edate) b
	--	本期領用
	declare @makeno nvarchar(max)
	declare @aa nvarchar(max)

	declare cursor_table cursor for
	select makeno
	from view_cubs a
	left join view_cub b on a.accy=b.accy and a.noa=b.noa
	where b.datea between @t_bdate and @t_edate
	and LEFT(a.makeno,1)='R'	
	open cursor_table
	fetch next from cursor_table
	into @makeno
	while(@@FETCH_STATUS <> -1)
	begin	
		set @aa = reverse(RIGHT(@makeno,len(@makeno)-1))
		if CHARINDEX('-',@aa)>0
		begin
			set @aa =reverse( RIGHT(@aa,LEN(@aa)-CHARINDEX('-',@aa)))
		end

		update @tmp set weight07=ISNULL(a.weight07,0) + isnull(b.x01,0)
			,total07=ISNULL(a.total07,0) + isnull(b.y01,0)
		from @tmp a
		outer apply(select * from @tmp where makeno=@makeno) b
		where a.makeno=@aa
		
		fetch next from cursor_table
		into @makeno
	end
	close cursor_table
	deallocate cursor_table
	-- 本期耗料 =  前期庫存 + 盤點調整 + 本期完工投入 - 本期樣品 - 本期報廢 - 本期領用 - 期末存貨
	update @tmp set weight04 = ISNULL(weight01,0) + ISNULL(weight02,0) +ISNULL(weight03,0) - ISNULL(weight05,0) - ISNULL(weight06,0)- ISNULL(weight07,0)- ISNULL(weight10,0)
		,total04 = ISNULL(total01,0) + ISNULL(total02,0) +ISNULL(total03,0) - ISNULL(total05,0) - ISNULL(total06,0)- ISNULL(total07,0)- ISNULL(total10,0)
		
	------------------------------------------------------------------------------------------	
	delete @tmp where not(len(@t_makeno)=0 or makeno=@t_makeno)
	--刪除重量都是0的
	delete @tmp where ISNULL(weight01,0)=0 and ISNULL(weight02,0)=0 and ISNULL(weight03,0)=0
		and ISNULL(weight04,0)=0 and ISNULL(weight05,0)=0 and ISNULL(weight06,0)=0 
		and ISNULL(weight07,0)=0 and ISNULL(weight10,0)=0
	--刪除都是0的
	delete @tmp where ISNULL(weight01,0)=0 and ISNULL(weight02,0)=0 and ISNULL(weight03,0)=0
		and ISNULL(weight04,0)=0 and ISNULL(weight05,0)=0 and ISNULL(weight06,0)=0 
		and ISNULL(weight07,0)=0 and ISNULL(weight10,0)=0
		and ISNULL(total01,0)=0 and ISNULL(total02,0)=0 and ISNULL(total03,0)=0
		and ISNULL(total04,0)=0 and ISNULL(total05,0)=0 and ISNULL(total06,0)=0 
		and ISNULL(total07,0)=0 and ISNULL(total10,0)=0

	update @tmp set price01 = case when ISNULL(weight01,0) = 0 then 0 else round(total01/weight01,4) end
		,price02 = case when ISNULL(weight02,0) = 0 then 0 else round(total02/weight02,4) end
		,price03 = case when ISNULL(weight03,0) = 0 then 0 else round(total03/weight03,4) end
		,price04 = case when ISNULL(weight04,0) = 0 then 0 else round(total04/weight04,4) end
		,price05 = case when ISNULL(weight05,0) = 0 then 0 else round(total05/weight05,4) end
		,price06 = case when ISNULL(weight06,0) = 0 then 0 else round(total06/weight06,4) end
		,price07 = case when ISNULL(weight07,0) = 0 then 0 else round(total07/weight07,4) end
		,price10 = case when ISNULL(weight10,0) = 0 then 0 else round(total10/weight10,4) end
	return
end