post:--post  get_rk	
	SET QUOTED_IDENTIFIER OFF 
	declare @cmd nvarchar(max) 
	declare @t_noa nvarchar(20) = [1]
	declare @t_condition nvarchar(10) = [2]
	declare @t_userno nvarchar(20) = [3]
	--------------------------------------------------------
	declare @t_accy nvarchar(20)=''
	--------------------------------------------------------
	DECLARE @chk tinyint = 0
	Begin Transaction [Trans_Name]
	
	begin try
		if @t_condition='0'
		begin
			--刪除入庫單
			select @t_accy=''
			select @t_accy=accy from view_get where noa=@t_noa
			set @cmd ="delete ina"+@t_accy+" where noa=@t_noa"
			execute sp_executesql @cmd,N'@t_noa nvarchar(20)',@t_noa=@t_noa
			IF @@Error <> 0 BEGIN SET @chk = 1 END
			set @cmd = "delete inas"+@t_accy+" where noa=@t_noa"
			execute sp_executesql @cmd,N'@t_noa nvarchar(20)',@t_noa=@t_noa
			IF @@Error <> 0 BEGIN SET @chk = 1 END
		end
		else if @t_condition='1'
		begin
			declare @datea nvarchar(10)=''
			declare @uno nvarchar(30) = ''
			declare @productno nvarchar(50) = ''
			declare @product nvarchar(50) = ''
			declare @mount float = 1
			declare @weight float = 0
			declare @price float = 0
			declare @money float = 0
			declare @memo nvarchar(max)=''
			declare @storeno nvarchar(20) = ''
			declare @store nvarchar(20) = ''
			
			select @t_accy=a.accy,@datea=a.datea,@uno=a.idno,@productno=a.productno,@product=a.product
				,@weight=a.[weight],@memo=a.memo,@storeno=a.stationno,@store=a.station
			from view_get a
			where noa=@t_noa
			and (len(ISNULL(a.idno,''))>0 or (len(a.productno)>0 and a.[weight]!=0))
			
			--有產品編號、重量的就產生批號
			if LEN(@uno)=0 and LEN(@productno)>0 and @weight!=0
			begin
				declare @maxuno1 nvarchar(30)=''
				declare @maxuno2 nvarchar(30)=''
				
				declare @sign nvarchar(max) = '0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'
				declare @key nvarchar(20) = ''
				declare @num nvarchar(20) = ''
				declare @n int
				declare @m int
				
				declare @xdate datetime
				set @xdate = dbo.ChineseEraName2AD(@datea)
				set @key =  right('00'+cast(year(@xdate) as nvarchar),2) + substring(@sign,month(@xdate)+1,1)
		
				--找出最大批號
				-- 16101-01
				select top 1 @maxuno1=uno from view_uccb where uno like ''+@key+'[0-9,A-Z][0-9,A-C]-[0-9,A-Z][0-9]' order by substring(uno,1,len(@key)+2) desc
				-- C16101-01
				select top 1 @maxuno2=uno from view_uccb where uno like '[A-Z]'+@key+'[0-9,A-Z][0-9,A-C]-[0-9,A-Z][0-9]' order by substring(uno,2,1+len(@key)+2) desc
				
				if SUBSTRING(isnull(@maxuno1,''),4,2)>=SUBSTRING(isnull(@maxuno2,''),5,2)
					set @num = SUBSTRING(@maxuno1,4,2)
				else 
					set @num = SUBSTRING(@maxuno2,5,2)
				if len(isnull(@num,''))=0
					set @num = '00'
				
				set @n = (charindex(LEFT(@num,1),@sign)-1)*10 + CAST(RIGHT(@num,1) as int) + 1
				set @num = SUBSTRING(@sign,floor(@n/10)+1,1)+SUBSTRING(@sign,(@n%10)+1,1)
				set @m = 1	
				set @uno=@key+@num+'-'+SUBSTRING(@sign,floor(@m/10)+1,1)+SUBSTRING(@sign,(@m%10)+1,1)		
				
				set @cmd = "update get"+@t_accy+" set idno=@uno where noa=@t_noa"
				execute sp_executesql @cmd,N'@t_noa nvarchar(20),@uno nvarchar(30)',@t_noa=@t_noa,@uno=@uno
				IF @@Error <> 0 BEGIN SET @chk = 1 END
			end
				
			begin try
				select @price= dbo.getsprice_rk(a.idno)
				from view_get a
				where noa=@t_noa
				and len(ISNULL(a.idno,''))>0
			end try
			begin catch
				--dbo.getsprice_rk()  當一直找不到時會出現巢狀錯誤,那時就當單價為0
			end catch
			
			set @money = round(@weight*@price,0)
			
			if len(@t_accy)>0
			begin
				--都當成物料
				set @cmd = "insert into ina"+@t_accy+"(datea,noa,itype,typea,kind,storeno,store)values(@datea,@t_noa,'1','生產入庫','F6',@storeno,@store)"
				execute sp_executesql @cmd,N'@datea nvarchar(20),@t_noa nvarchar(20),@storeno nvarchar(20),@store nvarchar(20)'
					,@datea=@datea,@t_noa=@t_noa,@storeno=@storeno,@store=@store
				IF @@Error <> 0 BEGIN SET @chk = 1 END
				set @cmd = "insert into inas"+@t_accy+"(noa,noq,uno,productno,product,mount,[weight],price,total,storeno,store,dime,width,lengthb,radius,memo)
					values(@t_noa,'001',@uno,@productno,@product,@mount,@weight,@price,@money,@storeno,@store,0,0,0,0,@memo)"
				execute sp_executesql @cmd,N'@t_noa nvarchar(20),@uno nvarchar(30),@productno nvarchar(50),@product nvarchar(100),@mount float,@weight float,@price float,@money float,@memo nvarchar(max),@storeno nvarchar(20),@store nvarchar(20)'
					,@t_noa=@t_noa,@uno=@uno,@productno=@productno,@product=@product,@mount=@mount,@weight=@weight,@price=@price,@money=@money,@memo=@memo,@storeno=@storeno,@store=@store
				IF @@Error <> 0 BEGIN SET @chk = 1 END
				--寫入UCCY
				if not exists(select * from uccy where uno=@uno)
				begin
					insert into uccy(uno)values(@uno)
					IF @@Error <> 0 BEGIN SET @chk = 1 END
				end
				
				insert into drun(datea,timea,usera,[action],noa,tablea,title,memo)
				select dbo.AD2ChineseEraName( CONVERT(nvarchar,getdate(),111))
					,LEFT(CONVERT(nvarchar,getdate(),108),5)
					,@t_userno
					,'Insert'
					,@t_noa
					,'ina'
					,''
					,'領料組合作業'
				IF @@Error <> 0 BEGIN SET @chk = 1 END
			end
		end
		
		IF @chk <> 0 BEGIN -- 若是新增資料發生錯誤
			Rollback Transaction [Trans_Name] -- 復原所有操作所造成的變更
		END
		ELSE BEGIN
			Commit Transaction [Trans_Name] -- 提交所有操作所造成的變更
		END
	end try
	begin catch
		Rollback Transaction [Trans_Name] -- 復原所有操作所造成的變更
		--有錯誤就都不執行
		insert into drun(datea,timea,usera,[action],noa,tablea,title,memo)
		select dbo.AD2ChineseEraName( CONVERT(nvarchar,getdate(),111))
			,LEFT(CONVERT(nvarchar,getdate(),108),5)
			,@t_userno
			,'get2ina錯誤'
			,@t_noa
			,'ina'
			,@t_condition
			,ERROR_MESSAGE()
	end catch;